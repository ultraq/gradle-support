/**
 * Common Gradle script configuration for bundling projects for standalone
 * download distributions.
 * 
 * @author Emanuel Rabina
 */

def isGroovyProject = plugins.hasPlugin('groovy')

/**
 * Create the source files archive.
 */
task sourcesZip(
	description: 'Create a zip file of all the project source files',
	type: Zip) {

	doFirst {
		baseName = "${project.artifact}"
	}
	from sourceSets.main.allSource
	destinationDir = file("${project.buildDir}/src")
	classifier = 'src'
}

/**
 * Creates a zip file for standalone downloads, containing:
 *  - the project 'binary'
 *  - the source files
 *  - folder of all required dependencies
 *  - folder of API documenation
 *  - and the license and readme files
 */
task bundle(
	description: 'Create the overall distribution archive',
	dependsOn: [jar, isGroovyProject ? groovydoc : javadoc, sourcesZip],
	type: Zip) {

	doFirst {
		archiveName = "${project.name} ${project.version}.zip"
	}
	from(jar.outputs.files)
	from(sourcesZip.outputs.files)
	from(configurations.runtime) {
		into 'libraries'
	}
	if (isGroovyProject) {
		from(groovydoc.destinationDir) {
			into 'groovydoc'
		}
	}
	else {
		from(javadoc.destinationDir) {
			into 'javadoc'
		}
	}
	from('.') {
		include 'LICENSE.txt'
		include 'README.md'
	}
	into project.name
}

/**
 * Make the bundle part of the assemble task.
 */
assemble {
	dependsOn bundle
}
