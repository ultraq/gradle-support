/**
 * Common Gradle script configuration for simple projects.  Made to be applied
 * _after_ all standard plugins.
 * 
 * @author Emanuel Rabina
 */

/* 
 * Introduce 'optional' and 'provided' configuration modifiers.
 */
project.ext.optionalDependencies = []
project.ext.optional = {
	optionalDependencies << it
}
project.ext.providedDependencies = []
project.ext.provided = {
	providedDependencies << it
}

/* 
 * Include Maven central and snapshot repositories by default.
 */
repositories {
	mavenCentral()
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}
}

/* 
 * Adjust plugins, source and build directories, and any standard Gradle tasks
 * for the way I use them.
 */
def sourceDir  = ['source']
def testDir    = ['tests']
def contentDir = 'Content'

if (plugins.hasPlugin('java')) {
	sourceSets {
		main {
			java.srcDirs      = sourceDir
			resources.srcDirs = sourceDir
			resources {
				exclude '**/*.java'
			}
			output.resourcesDir = output.classesDir
	
		}
		test {
			java.srcDirs        = testDir
			resources.srcDirs   = testDir
			output.resourcesDir = output.classesDir
		}
	}
	jar {
		dependsOn test
		duplicatesStrategy 'exclude'
	}
}

if (plugins.hasPlugin('groovy')) {
	sourceSets {
		main {
			groovy.srcDirs = sourceDir
			resources {
				exclude '**/*.groovy'
			}
		}
		test {
			groovy.srcDirs = testDir
		}
	}
	groovydoc {
		link('http://docs.oracle.com/javase/8/docs/api/', 'java.', 'javax.', 'org.xml.')
		link('http://docs.groovy-lang.org/latest/html/gapi/', 'groovy.', 'org.codehaus.groovy.')
	}
}
