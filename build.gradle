/*
 * Copyright 2024, Emanuel Rabina (http://www.ultraq.net.nz/)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'java-gradle-plugin'
	id 'groovy'
	id 'maven-publish'
}

description = 'Gradle support plugins for use in my own projects'
version = '5.0.0-SNAPSHOT'
group = 'nz.net.ultraq.gradle'

sourceSets {
	main {
		[java, groovy, resources]*.srcDirs = ['source']
		resources.exclude('**/*.groovy')
	}
	test {
		[java, groovy, resources]*.srcDirs = ['test']
		resources.exclude('**/*.groovy')
	}
}

repositories {
	mavenCentral()
	maven {
		url = 'https://central.sonatype.com/repository/maven-snapshots/'
	}
}

dependencies {
	testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
		vendor = JvmVendorSpec.ADOPTIUM
	}
}

gradlePlugin {
	website = 'https://github.com/ultraq/gradle-support'
	vcsUrl = 'https://github.com/ultraq/gradle-support.git'
	plugins {
		useMavenCentralRepositoriesPlugin {
			id = 'nz.net.ultraq.gradle.use-maven-central-repositories'
			implementationClass = 'nz.net.ultraq.gradle.UseMavenCentralRepositoriesPlugin'
			displayName = 'Use Maven Central and Snapshot repositories'
			description = '''
				Applies the Maven Central and Maven Central Snapshots repositories to the
				project configuration.  That's it ðŸ˜…  Can be used either in `settings.gradle`
				to apply across all projects, or `build.gradle` to apply to a single project.
			'''
			tags = ['maven', 'repository']
		}
		singleSourceDirectoryPlugin {
			id = 'nz.net.ultraq.gradle.single-source-directory'
			implementationClass = 'nz.net.ultraq.gradle.SingleSourceDirectoryPlugin'
			displayName = ''
			description = '''
				Make the source and resources directories be the same directory.  This is to
				help those that prefer having source code and assets co-located so you're not
				jumping around the file tree looking for related items.
			'''
			tags = ['sources', 'resources']
		}
		groovySupportPlugin {
			id = 'nz.net.ultraq.gradle.groovy-support'
			implementationClass = 'nz.net.ultraq.gradle.GroovySupportPlugin'
			displayName = 'Groovy artifact configuration for Maven Central and other services'
			description = '''
				Works in tandem with Gradle's built-in `groovy` plugin to help Groovy
				projects achieve configuration parity with Java projects.  Mainly, by
				allowing Groovy outputs/artifacts to participate in all the usual lifecycle
				tasks.
			'''
			tags = ['groovy', 'groovydoc']
		}
		developmentPlugin {
			id = 'nz.net.ultraq.gradle.development-plugin'
			implementationClass = 'nz.net.ultraq.gradle.DevelopmentPlugin'
		}
		publishingPlugin {
			id = 'nz.net.ultraq.gradle.publishing-plugin'
			implementationClass = 'nz.net.ultraq.gradle.PublishingPlugin'
		}
	}
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}
	}
}

publishing {
	repositories {
		maven {
			url = file('../test-maven-repository')
		}
	}
}
