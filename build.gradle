/*
 * Copyright 2024, Emanuel Rabina (http://www.ultraq.net.nz/)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'java-gradle-plugin'
	id 'groovy'
	id 'jacoco'
	id 'maven-publish'
	id 'signing'
	id 'com.gradle.plugin-publish' version '1.3.1'
}

description = 'Gradle support plugins for use in my own projects'
version = '0.2.0'
group = 'nz.net.ultraq.gradle'

sourceSets {
	main {
		[java, groovy, resources]*.srcDirs = ['source']
		resources.exclude('**/*.groovy')
	}
	test {
		[java, groovy, resources]*.srcDirs = ['test']
		resources.exclude('**/*.groovy')
	}
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

afterEvaluate { ->
	tasks.named('sourcesJar') {
		duplicatesStrategy = 'exclude'
	}
}

gradlePlugin {
	website = 'https://github.com/ultraq/gradle-support'
	vcsUrl = 'https://github.com/ultraq/gradle-support.git'
	plugins {
		useMavenCentralRepositoriesPlugin {
			id = 'nz.net.ultraq.gradle.use-maven-central-repositories'
			implementationClass = 'nz.net.ultraq.gradle.UseMavenCentralRepositoriesPlugin'
			displayName = 'Use Maven Central and Snapshot repositories'
			description =
				'Applies the Maven Central and Snapshot repositories to the project configuration.  Can ' +
				'be used either in `settings.gradle` to apply across all projects, or `build.gradle` to ' +
				'apply to a single project.'
			tags = ['maven', 'repository']
		}
		fluentConfigurationPlugin {
			id = 'nz.net.ultraq.gradle.fluent-configuration'
			implementationClass = 'nz.net.ultraq.gradle.FluentConfigurationPlugin'
			displayName = 'A fluent API for configuring projects'
			description =
				'Adds a `configure` script block to a `build.gradle` file, within which a fluent API can ' +
				'be used to configure a project.'
			tags = ['fluent', 'configuration']
		}
	}
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}
	}
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
}

publishing {
	repositories {
		maven {
			url = file('../test-maven-repository')
		}
	}
}
