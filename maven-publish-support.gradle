/*
 * Copyright 2020, Emanuel Rabina (http://www.ultraq.net.nz/)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Common Gradle script configuration for projects that need to publish to
 * Maven repositories.  Uses the newer maven-publish plugin.
 *
 * @author Emanuel Rabina
 */

apply plugin: 'signing'

project.ext.year = ''
project.ext.licenses = [
	[
		name: 'The Apache Software License, Version 2.0',
		url: 'http://www.apache.org/licenses/LICENSE-2.0.txt',
		distribution: 'repo'
	]
]
project.ext.developers = []

/**
 * Also put together javadoc and source JARs for upload.
 */
java {
	if (plugins.hasPlugin('groovy')) {
		tasks.named('assemble') {
			dependsOn groovydocJar
		}
	}
	else if (plugins.hasPlugin('java')) {
		withJavadocJar()
	}
	withSourcesJar()
}

tasks.named('sourcesJar') {
	duplicatesStrategy = 'exclude'
}

/**
 * Publishing configuration to customize the pom file
 */
publishing {
	publications {
		library(MavenPublication) {
			from components.java
			if (plugins.hasPlugin('groovy')) {
				artifact(groovydocJar) {
					classifier = 'javadoc'
				}
			}
			afterEvaluate {
				pom {
					name = project.name
					description = project.description
					url = "https://github.com/ultraq/${project.rootProject.name}/"
					inceptionYear = project.year
					licenses {
						project.licenses.each { l ->
							license {
								name = l.name
								url = l.url
								distribution = l.distribution
							}
						}
					}
					scm {
						connection = "scm:git:git@github.com:ultraq/${project.rootProject.name}.git"
						developerConnection = "scm:git:git@github.com:ultraq/${project.rootProject.name}.git"
						url = "https://github.com/ultraq/${project.rootProject.name}"
					}
					developers {
						developer {
							id = 'ultraq'
							name = 'Emanuel Rabina'
							email = 'emanuelrabina@gmail.com'
							url = 'http://www.ultraq.net.nz/'
							roles = [
								'author',
								'developer'
							]
							timezone = 'Pacific/Auckland'
						}
						project.developers.each { d ->
							developer {
								name = d.name
								email = d.email
								url = d.url
								roles = d.roles
							}
						}
					}
				}
			}
		}
	}
	repositories {
		maven {
			afterEvaluate {
				url = project.version.endsWith('SNAPSHOT') ?
					'https://central.sonatype.com/repository/maven-snapshots/' :
					'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
			}
			credentials {
				username = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ''
				password = project.hasProperty('sonatypePassword') ? sonatypePassword : ''
			}
		}
	}
}

/**
 * Sign the JARs that will be uploaded to Maven central.
 */
signing {
	sign publishing.publications.library
}
